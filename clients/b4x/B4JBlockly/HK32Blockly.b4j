AppType=JavaFX
Build1=Default,b4j.example
File1=MainLayout.bjl
FileGroup1=Default Group
Group=Default Group
Library1=javaobject
Library2=jcore
Library3=jfx
Library4=json
Library5=xui views
NumberOfFiles=1
NumberOfLibraries=5
NumberOfModules=0
Version=10.3
@EndOfDesignText@
#Region  Project Attributes 
    #MainFormWidth: 1000
    #MainFormHeight: 700
#End Region

Sub Process_Globals
	Private VERSION As String = "HK32Blockly v20251209"
	Public CAPPLOG As Boolean = True

	Private fx As JFX
	Private MainForm As Form

	' UI elements from MainLayout.fxml
	Private WebViewBlockly As WebView
	Private btnExecute As Button
	Private ClvLog As CustomListView
	Private btnClearLog As B4XView
End Sub

' ------------------------------
' AppStart
' ------------------------------
Sub AppStart (Form1 As Form, Args() As String)
	Log($"[Main.AppStart] ${VERSION}"$)

	MainForm = Form1
	MainForm.Title = VERSION
	MainForm.RootPane.LoadLayout("MainLayout") ' contains WebViewBlockly, lvCommands, btnExecute
	MainForm.Show

	AppInit
End Sub

' ------------------------------
' AppInit - just load the HTML file
' ------------------------------
Sub AppInit
	AppLog("Initializing WebView Blockly...")

	' Load Blockly HTML. JS will be injected in PageFinished (after load).
	Dim htmlUri As String = File.GetUri(File.DirApp, "blockly_index.html") ' adjust if needed
	AppLog("Loading Blockly HTML from: " & htmlUri)
	WebViewBlockly.LoadUrl(htmlUri)
	' 
	ClvLog.Clear
End Sub

' ------------------------------
' WebView events (optional)
' ------------------------------
Private Sub WebViewBlockly_LocationChanged (Location As String)
End Sub

' ------------------------------
' Page finished: inject bridge and console log redirection
' ------------------------------
Private Sub WebViewBlockly_PageFinished (Url As String)
	AppLog("[WebViewBlockly_PageFinished] url=" & Url)

	' Create the webview engine object
	Dim engine As JavaObject = GetEngine(WebViewBlockly)

	' Set javascript enabled for the webengine
	engine.RunMethod("setJavaScriptEnabled", Array(True))

'	' Create an EventHandler so JS alert() calls come to WebViewBlockly_Event.
'	' (This makes JS alert messages appear in your B4J logs via WebViewBlockly_Event.)
	Dim ev As Object = engine.CreateEvent("javafx.event.EventHandler", "WebViewBlockly", False)
	engine.RunMethod("setOnAlert", Array(ev))
	AppLog("[WebViewBlockly_PageFinished] alert redirection for receiving generated code.")
End Sub

' This receives JS alert() messages (via setOnAlert event handler)
Sub WebViewBlockly_Event(MethodName As String, Args() As Object)
	If Args.Length > 0 Then
		Dim joWebEvent As JavaObject = Args(0)
		Dim msg As String = joWebEvent.RunMethod("getData", Null)
		AppLog(msg)
		' msg = msg.Replace($"\""$, $"""$)
		AppLog("[WebViewBlockly_Event]: " & msg) ' Alerts from web page show here
		Try
			Dim parser As JSONParser
			parser.Initialize(msg)
			Dim jRoot As Map = parser.NextObject
			Dim command As String = jRoot.Get("command")
			If command == "start" Then ClvLog.Clear
			ClvLog.AddTextItem(command, command)
			AppLog($"[WebViewBlockly_Event] ${command}"$)
		Catch
			AppLog($"[WebViewBlockly_Event][E] ${LastException}"$)
		End Try
	End If
End Sub

' ------------------------------
' Execute button - user triggers sending command to hardware
' ------------------------------
Sub btnExecute_Click
	' Create the webview engine object
	Dim engine As JavaObject = GetEngine(WebViewBlockly)
	Dim obj As Object = engine.RunMethod("executeScript", Array("updateDeviceState('yellow_led', 'ON')"))
	Dim tempValue As Float = 22.3
	Dim humValue As Float = 54
	obj = engine.RunMethod("executeScript", Array($"updateDeviceDHT11(${tempValue},${humValue})"$))
	Log(obj)
End Sub

' ------------------------------
' Logging Helper
' ------------------------------
Private Sub btnClearLog_Click
	ClvLog.Clear	
End Sub

Public Sub AppLog(sEntry As String)
	If CAPPLOG Then
		Log(DateTime.Date(DateTime.Now) & " " & DateTime.Time(DateTime.Now) & ": " & sEntry)
	End If
End Sub

' ------------------------------
' Helper to access the WebEngine via JavaObject
' ------------------------------
Sub GetEngine(wv As WebView) As JavaObject
	Dim jo As JavaObject = wv
	Return jo.RunMethod("getEngine", Null)
End Sub


